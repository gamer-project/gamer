reset


# target files
# -----------------------------------------------------------
FILE_REF        = 'ReferenceSolution/Gamma_1.67/RJ2a'
FILE_IN_PREFIX  = 'Xline_y0.000_z0.000'
FILE_IN_SUFFIX  = ''
FILE_OUT_PREFIX = 'Fig__Riemann_MHD'
DATA_ID         = 20


# terminal
# -----------------------------------------------------------
 set term x11 enhanced
#set term png enhanced


# plot style
# -----------------------------------------------------------
 set key reverse Left spacing 1.5 right
 set xlabel "r"
 set pointsize 1.0
#set size square
 set xrange [-0.01:1.01]
#set yrange [0.0:1.1]


# set the output file
# -----------------------------------------------------------
if      ( GPVAL_TERM eq 'pngcairo'   ) FILE_OUT_EXT='png'; \
else if ( GPVAL_TERM eq 'png'        ) FILE_OUT_EXT='png'; \
else if ( GPVAL_TERM ne 'x11'        ) print 'Unkown terminal for settting the output file extension !!' quit


# plot
# -----------------------------------------------------------
FILE_IN = sprintf( '%s_%06d%s', FILE_IN_PREFIX, DATA_ID, FILE_IN_SUFFIX )

# density
set ylabel "Density"
if ( GPVAL_TERM ne 'x11' ) { set output sprintf( '%s_%06d_Dens.%s', FILE_OUT_PREFIX, DATA_ID, FILE_OUT_EXT ) }
plot FILE_IN  u 4:7 w p lc  7 pt 6 tit 'Simulation' \
    ,FILE_REF u 1:2 w l lc -1      tit 'Analytical'
if ( GPVAL_TERM eq 'x11' ) { pause mouse }

# Vx
set ylabel "Vx"
if ( GPVAL_TERM ne 'x11' ) { set output sprintf( '%s_%06d_VelX.%s', FILE_OUT_PREFIX, DATA_ID, FILE_OUT_EXT ) }
plot FILE_IN  u 4:($8/$7) w p lc  7 pt 6 tit 'Simulation' \
    ,FILE_REF u 1:3       w l lc -1      tit 'Analytical'
if ( GPVAL_TERM eq 'x11' ) { pause mouse }

# Vy
set ylabel "Vy"
if ( GPVAL_TERM ne 'x11' ) { set output sprintf( '%s_%06d_VelY.%s', FILE_OUT_PREFIX, DATA_ID, FILE_OUT_EXT ) }
plot FILE_IN  u 4:($9/$7) w p lc  7 pt 6 tit 'Simulation' \
    ,FILE_REF u 1:4       w l lc -1      tit 'Analytical'
if ( GPVAL_TERM eq 'x11' ) { pause mouse }

# Vz
set ylabel "Vz"
if ( GPVAL_TERM ne 'x11' ) { set output sprintf( '%s_%06d_VelZ.%s', FILE_OUT_PREFIX, DATA_ID, FILE_OUT_EXT ) }
plot FILE_IN  u 4:($10/$7) w p lc  7 pt 6 tit 'Simulation' \
    ,FILE_REF u 1:5        w l lc -1      tit 'Analytical'
if ( GPVAL_TERM eq 'x11' ) { pause mouse }

# pressure
set ylabel "Pressure"
if ( GPVAL_TERM ne 'x11' ) { set output sprintf( '%s_%06d_Pres.%s', FILE_OUT_PREFIX, DATA_ID, FILE_OUT_EXT ) }
plot FILE_IN  u 4:16 w p lc  7 pt 6 tit 'Simulation' \
    ,FILE_REF u 1:9  w l lc -1      tit 'Analytical'
if ( GPVAL_TERM eq 'x11' ) { pause mouse }

# Bx
set ylabel "Bx"
if ( GPVAL_TERM ne 'x11' ) { set output sprintf( '%s_%06d_MagX.%s', FILE_OUT_PREFIX, DATA_ID, FILE_OUT_EXT ) }
plot FILE_IN  u 4:12 w p lc  7 pt 6 tit 'Simulation' \
    ,FILE_REF u 1:6  w l lc -1      tit 'Analytical'
if ( GPVAL_TERM eq 'x11' ) { pause mouse }

# By
set ylabel "By"
if ( GPVAL_TERM ne 'x11' ) { set output sprintf( '%s_%06d_MagY.%s', FILE_OUT_PREFIX, DATA_ID, FILE_OUT_EXT ) }
plot FILE_IN  u 4:13 w p lc  7 pt 6 tit 'Simulation' \
    ,FILE_REF u 1:7  w l lc -1      tit 'Analytical'
if ( GPVAL_TERM eq 'x11' ) { pause mouse }

# Bz
set ylabel "Bz"
if ( GPVAL_TERM ne 'x11' ) { set output sprintf( '%s_%06d_MagZ.%s', FILE_OUT_PREFIX, DATA_ID, FILE_OUT_EXT ) }
plot FILE_IN  u 4:14 w p lc  7 pt 6 tit 'Simulation' \
    ,FILE_REF u 1:8  w l lc -1      tit 'Analytical'
if ( GPVAL_TERM eq 'x11' ) { pause mouse }


if ( GPVAL_TERM eq 'x11' ) { pause -1 }
