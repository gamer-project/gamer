reset


# target files
# -----------------------------------------------------------
FILE_REF        = 'ReferenceSolution/TM_EoS/SRHD_UR'
FILE_IN_PREFIX  = 'Xline_y0.000_z0.000'
FILE_IN_SUFFIX  = ''
FILE_OUT_PREFIX = 'Fig__Riemann_SRHD'
DATA_ID         = 100


# terminal
# -----------------------------------------------------------
 set term x11 enhanced
# set term png enhanced


# plot style
# -----------------------------------------------------------
 set key reverse Left spacing 1.5 right
 set xlabel "r"
 set pointsize 1.0
#set size square
 set xrange [-0.01:1.01]
#set yrange [0.0:1.1]


# set the output file
# -----------------------------------------------------------
if      ( GPVAL_TERM eq 'pngcairo'   ) FILE_OUT_EXT='png'; \
else if ( GPVAL_TERM eq 'png'        ) FILE_OUT_EXT='png'; \
else if ( GPVAL_TERM ne 'x11'        ) print 'Unkown terminal for settting the output file extension !!' quit


# plot
# -----------------------------------------------------------
FILE_IN = sprintf( '%s_%06d%s', FILE_IN_PREFIX, DATA_ID, FILE_IN_SUFFIX )

# Rho
set ylabel "Rho"
set log y
if ( GPVAL_TERM ne 'x11' ) { set output sprintf( '%s_%06d_Rho.%s', FILE_OUT_PREFIX, DATA_ID, FILE_OUT_EXT ) }
plot FILE_IN  u 4:($7/$13) w p lc  7 pt 6 tit 'Simulation' \
    ,FILE_REF u 1:2        w l lc -1      tit 'Analytical'
if ( GPVAL_TERM eq 'x11' ) { pause mouse }

# Ux/c
set ylabel "Ux/c"
unset log y
if ( GPVAL_TERM ne 'x11' ) { set output sprintf( '%s_%06d_UX.%s', FILE_OUT_PREFIX, DATA_ID, FILE_OUT_EXT ) }
plot FILE_IN  u 4:($14*$13) w p lc  7 pt 6 tit 'Simulation' \
    ,FILE_REF u 1:3         w l lc -1      tit 'Analytical'
if ( GPVAL_TERM eq 'x11' ) { pause mouse }

# Uy/c
set ylabel "Uy/c"
unset log y
if ( GPVAL_TERM ne 'x11' ) { set output sprintf( '%s_%06d_UY.%s', FILE_OUT_PREFIX, DATA_ID, FILE_OUT_EXT ) }
plot FILE_IN  u 4:($15*$13) w p lc  7 pt 6 tit 'Simulation' \
    ,FILE_REF u 1:4         w l lc -1      tit 'Analytical'
if ( GPVAL_TERM eq 'x11' ) { pause mouse }

# Uz/c
set ylabel "Uz/c"
unset log y
if ( GPVAL_TERM ne 'x11' ) { set output sprintf( '%s_%06d_UZ.%s', FILE_OUT_PREFIX, DATA_ID, FILE_OUT_EXT ) }
plot FILE_IN  u 4:($16*$13) w p lc  7 pt 6 tit 'Simulation' \
    ,FILE_REF u 1:5         w l lc -1      tit 'Analytical'
if ( GPVAL_TERM eq 'x11' ) { pause mouse }

# pressure
set ylabel "Pressure"
set log y
if ( GPVAL_TERM ne 'x11' ) { set output sprintf( '%s_%06d_Pres.%s', FILE_OUT_PREFIX, DATA_ID, FILE_OUT_EXT ) }
plot FILE_IN  u 4:12 w p lc  7 pt 6 tit 'Simulation' \
    ,FILE_REF u 1:6  w l lc -1      tit 'Analytical'
if ( GPVAL_TERM eq 'x11' ) { pause mouse }


if ( GPVAL_TERM eq 'x11' ) { pause -1 }
